apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.getkeepsafe.relinker:relinker:1.2.2'
}

def GROUP = 'me.ele'
def ARTIFACTID = 'bspatch'

group = GROUP
version = VERSION

install {
    repositories.mavenInstaller {
        pom.project {
            name ARTIFACTID
            description 'A bspatch library for Android platform'
            url 'https://github.com/eleme/bspatch'
            inceptionYear '2016'

            packaging 'aar'
            groupId GROUP
            artifactId ARTIFACTID

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }

            }
            developers {
                developer {
                    id "jack_1900"
                    name "Jack Cho"
                    email "jack_1900@163.com"
                }
            }
        }
    }
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = ARTIFACTID
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/eleme/bspatch'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = files(project.android.getBootClasspath())
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

